import java.awt.Desktop

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.1'
    }
}

apply plugin: 'war'
apply plugin: 'liberty'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "ch.ocram.tpmanprototype"
version = "0.0.1-SNAPSHOT"
description = "tpman-prototype / master-data"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.yaml:snakeyaml:1.19'
    implementation 'com.google.guava:guava:24.0-jre'
    compileOnly 'javax:javaee-api:7.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.15.0'
    testImplementation 'org.assertj:assertj-core:3.9.0'
    testImplementation 'org.apache.cxf:cxf-rt-rs-client:3.1.11'
    testImplementation 'org.apache.cxf:cxf-rt-rs-extension-providers:3.1.11'
    testImplementation 'javax.json:javax.json-api:1.0'

    libertyRuntime group: 'io.openliberty', name: 'openliberty-runtime', version: '[17.0.0.4,)'
}

ext {
    appName = project.name
    testServerHttpPort = 9080
    testServerHttpsPort = 9443
    warContext = appName
}

liberty {
    server {
        name = "${appName}Server"
        configFile = file("src/main/liberty/config/server.xml")
        bootstrapProperties = ['default.http.port' : testServerHttpPort,
                               'default.https.port': testServerHttpsPort,
                               'app.context.root'  : warContext] 
        packageLiberty {
            archive = "$buildDir/${appName}.zip"
            include = "usr"
        }
    }
}

war {
    archiveName = "${baseName}.${extension}"
}


test {
    systemProperties = ['liberty.test.port': testServerHttpPort, 'war.name': warContext]
}

task openBrowser {
    description = 'Open browser to http://localhost:8080/${warContext}'
    doLast {
        Desktop.desktop.browse "http://localhost:${testServerHttpPort}/${appName}".toURI()
    }
}

check.dependsOn 'libertyStart'
check.finalizedBy 'libertyStop'

